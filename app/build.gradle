apply plugin: 'com.android.application'

repositories {
    maven { url "https://jitpack.io" }
    jcenter()
}


buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }

    }
}

// set flag whether we are on a build server
def isCi = "true" == System.getenv("CI")

android {
    compileSdkVersion 28
    signingConfigs {
        debug {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("${project.rootDir}/signing/debug.keystore")
            storePassword 'android'
        }
        release {
            if (isCi) {
                storeFile rootProject.file("signing/release.jks")
                storePassword = System.getenv("RELEASE_STORE_PASSWORD")
                keyAlias = System.getenv("RELEASE_ALIAS")
                keyPassword = System.getenv("RELEASE_PASSWORD")
            } else {
                Properties propsSigning = new Properties()
                def signingPropsFile = rootProject.file("${project.rootDir}/signing/release.signing.properties")
                if (signingPropsFile.canRead()) {
                    propsSigning.load(new FileInputStream(signingPropsFile))
                    if (propsSigning != null &&
                            propsSigning.containsKey('STORE_FILE') &&
                            propsSigning.containsKey('STORE_PASSWORD') &&
                            propsSigning.containsKey('KEY_ALIAS') &&
                            propsSigning.containsKey('KEY_PASSWORD')) {
                        storeFile = file(propsSigning['STORE_FILE'])
                        storePassword = propsSigning['STORE_PASSWORD']
                        keyAlias = propsSigning['KEY_ALIAS']
                        keyPassword = propsSigning['KEY_PASSWORD']
                    } else {
                        println 'release.signing.properties file found but some entries are missing'
                    }
                } else {
                    println 'release.signing.properties file is missing'
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    defaultConfig {
        applicationId "dynoapps.exchange_rates"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode getBuildVersionCode()
        versionName getBuildVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            versionNameSuffix ' Debug'
            applicationIdSuffix '.debug'
            signingConfig signingConfigs.debug
            minifyEnabled false
        }
        production {
            proguardFile 'proguard-appcompat-v7.pro'
            proguardFile 'proguard-android-java.pro'
            proguardFile 'proguard-square-retrofit2.pro'
            proguardFile 'proguard-simple-xml.pro'
            proguardFile 'proguard-rules.pro'
            proguardFile 'proguard-project-app.pro'
            proguardFile 'proguard-support-design.pro'
            signingConfig signingConfigs.release
            minifyEnabled true;
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha03'

    implementation 'com.google.android.gms:play-services-analytics:16.0.7'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.13.1'

    implementation('com.squareup.retrofit2:converter-simplexml:2.5.0') {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    // reactiveX
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'

    // retrofit
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'


    implementation 'com.jakewharton:butterknife:10.0.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.0.0'

    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.1'
    implementation 'com.github.orhanobut:logger:2.2.0'

    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'

    implementation 'androidx.room:room-runtime:2.1.0-alpha04'
    implementation 'androidx.room:room-rxjava2:2.1.0-alpha04'
    implementation 'androidx.lifecycle:lifecycle-runtime:2.1.0-alpha02'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0-alpha02'
    annotationProcessor 'androidx.room:room-compiler:2.1.0-alpha04'


    androidTestImplementation('tools.fastlane:screengrab:1.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })


    testImplementation 'junit:junit:4.12'


    androidTestImplementation 'androidx.annotation:annotation:1.1.0-alpha01'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'
    androidTestImplementation 'androidx.test:rules:1.1.2-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.2-alpha01'
    androidTestImplementation 'org.hamcrest:hamcrest-library:2.1'


}

def getPropertiesFile() {
    return file("${project.rootDir}/gradle.properties")
}

def getBuildVersionName() {
    Properties properties = new Properties()
    def propsFile = getPropertiesFile()
    if (propsFile.canRead()) {

        properties.load(propsFile.newDataInputStream())
        def major = properties.getProperty("versionMajor") as String
        def minor = properties.getProperty("versionMinor") as String
        def patch = properties.getProperty("versionPatch") as String
        def build = properties.getProperty("versionBuild") as String
        def suffix = properties.get("versionSuffix") != null ? properties.getProperty("versionSuffix") : "" as String
        println "Suffix : " + suffix
        def version = major + "." + minor
        if (build != "0") {
            version += "." + patch + "." + build
        } else if (patch != "0") {
            version += "." + patch
        }
        return version + suffix

    } else {
        throw new GradleException("Versionname : unable to find gradle.properties file : \n" + propsFile.absolutePath)
    }
}

def getBuildVersionCode() {

    Properties properties = new Properties()
    def propsFile = getPropertiesFile()

    if (propsFile.canRead()) {
        properties.load(propsFile.newDataInputStream())

        def major = properties.getProperty("versionMajor") as int
        def minor = properties.getProperty("versionMinor") as int
        def patch = properties.getProperty("versionPatch") as int
        def build = properties.getProperty("versionBuild") as int
        def suffix = properties.get("versionSuffix") != null ? properties.getProperty("versionSuffix") : "" as String
        String[] suffixSplited = suffix.tokenize(".")
        int suffixInt = suffixSplited.size() > 1 ? suffixSplited[1] as int : 0 as int
        return (major * 1000000 + minor * 10000 + patch * 100 + build + suffixInt) as int
    } else {
        println 'unable to find gradle.properties file'
        throw new GradleException("Versioncode : unable to find gradle.properties file : " + propsFile.absolutePath)
    }
}
